;----------------------------------------------------------------------------
; Copyright (C) 2006 Arturo Ragozini and Daniel Vik
;
; This software is provided 'as-is', without any express or implied
; warranty. In no event will the authors be held liable for any damages
; arising from the use of this software.
;
; Permission is granted to anyone to use this software for any purpose,
; including commercial applications, and to alter it and redistribute it
; freely, subject to the following restrictions:
;
; 1. The origin of this software must not be misrepresented; you must not
; claim that you wrote the original software. If you use this software
; in a product, an acknowledgment in the product documentation would be
; appreciated but is not required.
; 2. Altered source versions must be plainly marked as such, and must not be
; misrepresented as being the original software.
; 3. This notice may not be removed or altered from any source distribution.
;----------------------------------------------------------------------------

; Modified in 2016 by Maxim for operation on Sega 8-bit consoles (and similar 
; hardware with IO-mapped SN76489 variants)

;
; Replayer core to play RLE encoded 8.000kHz samples generated by pcmenc
;
; pcmenc should use the following command line arguments:
;
; pcmenc -dt1 12 -dt2 12 -dt3 423 file.wav
;
; and optionally -r to split sample into blocks for rom replayer
;

; There are three channel updates per underlying sample.
; We emit three channel updates together, as fast as possible, looping
; every 447 cycles, to match an underlying sample at 8008Hz
; (3579545/447=8007.9).
; Work costs 309 cycles so we waste 139.

;-------------------------------------
; Plays one sample
; IN HL - Encoded sample start address
; IX - Sample length (#pcm samples)
;-------------------------------------
PLAY_SAMPLE:
  ld de,0
  ld bc,0

PsgLoop:
; Calculate channel A volume
  ld a,b          ; 4
  sub $10         ; 7
  jr nc,PsgWaitA  ; 7/12
  ld a,(hl)       ; 7
  inc hl          ; 6
  ld b,a          ; 4
  and $0f         ; 7
  or $90          ; 7
  ld iyh,a        ; 8 -> 57
PsgDoneA:
  
; Calculate channel B volume
  ld a,d          ; 4
  sub $10         ; 7
  jr nc,PsgWaitB  ; 7/12
  ld a,(hl)       ; 7
  inc hl          ; 6
  ld d,a          ; 4
  and $0f         ; 7
  or $b0          ; 7
  ld iyl,a        ; 8 -> 57
PsgDoneB:
  
; Calculate channel C volume
  ld a,e          ; 4
  sub $10         ; 7
  jr nc,PsgWaitC  ; 7/12
  ld a,(hl)       ; 7
  inc hl          ; 6
  ld e,a          ; 4
  and $0f         ; 7
  or $d0          ; 7
  ld c,a          ; 4 -> 53
PsgDoneC:

  ; Output channels
  push de         ; 11
    ld d,iyh      ;  8
    ld e,iyl      ;  8
    ld a,c        ;  4
    push bc       ; 11
      ld c,$7f    ;  7 -> 49
      
      out (c),d   ; 12
      out (c),e   ; 12
      out (c),a   ; 12
    pop bc        ; 10
  pop de          ; 10 -> 66
  
  ; Waste cycles
  call Delay
  
  ; Decrement length and return if zero
  dec ix          ; 10
  ld a,ixh        ; 8
  or ixl          ; 8
  jp nz,PsgLoop   ; 10 -> 36
  ret
  
PsgWaitA:
  ld b,a      ;  4
  ld a,i      ;  9
  ld a,i      ;  9
  jr PsgDoneA ; 12 -> 34
  
PsgWaitB:
  ld d,a      ;  4
  ld a,i      ;  9
  ld a,i      ;  9
  jr PsgDoneB ; 12 -> 34
  
PsgWaitC:
  ld e,a      ;  4
  and 0       ;  7
  and 0       ;  7
  jr PsgDoneC ; 12 -> 30

Delay:
  ; call Delay   17
  push bc     ;  11
    ld b,6    ;   7
-:  djnz -    ;  13*5+8
  pop bc      ;  10
  in a,($7f)  ;  11
  ret         ;  10
              ; ---
              ; 139
