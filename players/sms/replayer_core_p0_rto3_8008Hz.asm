;----------------------------------------------------------------------------
; Copyright (C) 2006 Arturo Ragozini and Daniel Vik
;
; This software is provided 'as-is', without any express or implied
; warranty. In no event will the authors be held liable for any damages
; arising from the use of this software.
;
; Permission is granted to anyone to use this software for any purpose,
; including commercial applications, and to alter it and redistribute it
; freely, subject to the following restrictions:
;
; 1. The origin of this software must not be misrepresented; you must not
; claim that you wrote the original software. If you use this software
; in a product, an acknowledgment in the product documentation would be
; appreciated but is not required.
; 2. Altered source versions must be plainly marked as such, and must not be
; misrepresented as being the original software.
; 3. This notice may not be removed or altered from any source distribution.
;----------------------------------------------------------------------------

; Modified in 2016 by Maxim for operation on Sega 8-bit consoles (and similar 
; hardware with IO-mapped SN76489 variants)

;
; Replayer core to play RLE encoded 8008Hz samples generated by pcmenc
;
; pcmenc should use the following command line arguments:
;
; pcmenc -rto 3 -dt1 447 -dt2 447 -dt3 447 file.wav
;
; and optionally -r to split sample into blocks for rom replayer
;

; There is one channel update per underlying sample.
; We want them to be 447 cycles apart.

;-------------------------------------
; Plays one sample
; HL - pointes to triplet count followed by data
;-------------------------------------
PLAY_SAMPLE:
  ld de,0
  ld bc,0
  ; get the triplet count
  ld a, (hl)
  inc hl
  ld ixl, a
  ld a, (hl)
  inc hl
  ld ixh, a

PsgLoop:
; Calculate channel A volume
  ld a,b          ; 4
  sub $10         ; 7
  jr nc,PsgWaitA  ; 7/12
  ld a,(hl)       ; 7
  inc hl          ; 6
  ld b,a          ; 4
  and $0f         ; 7
  or $90          ; 7
  out ($7f),a     ; 11 -> 60
PsgDoneA:

  call Delay387
  
; Calculate channel B volume
  ld a,d          ; 4
  sub $10         ; 7
  jr nc,PsgWaitB  ; 7/12
  ld a,(hl)       ; 7
  inc hl          ; 6
  ld d,a          ; 4
  and $0f         ; 7
  or $b0          ; 7
  out ($7f),a     ; 11 -> 60
PsgDoneB:

  call Delay387
  
; Calculate channel C volume
  ld a,e          ; 4
  sub $10         ; 7
  jr nc,PsgWaitC  ; 7/12
  ld a,(hl)       ; 7
  inc hl          ; 6
  ld e,a          ; 4
  and $0f         ; 7
  or $d0          ; 7
  out ($7f),a     ; 11 -> 60
PsgDoneC:

  call Delay351
  
  ; Decrement length and return if zero
  dec ix          ; 10
  ld a,ixh        ; 8
  or ixl          ; 8
  jp nz,PsgLoop   ; 10 -> 36
  ret
  
PsgWaitA:
  ld b,a      ;  4
  push hl     ; 11
  pop hl      ; 10
  jr PsgDoneA ; 12 -> 37
  
PsgWaitB:
  ld d,a      ;  4
  push hl     ; 11
  pop hl      ; 10
  jr PsgDoneB ; 12 -> 37
  
PsgWaitC:
  ld e,a      ;  4
  push hl     ; 11
  pop hl      ; 10
  jr PsgDoneC ; 12 -> 37

Delay387:
  ; call Delay   17
  push bc     ;  11
    ld b,25   ;   7
-:  djnz -    ;  13*24+8
    in b,(c)  ;  12
  pop bc      ;  10
  ret         ;  10
              ; ---
              ; 387

Delay351:
  ; call         17
  push bc     ;  11
    ld b,22   ;   7
-:  djnz -    ;  13*21+8
  pop bc      ;  10
  ld a,ixh    ;   8
  ld a,0      ;   7
  ret         ;  10
              ; ---
              ; 351
